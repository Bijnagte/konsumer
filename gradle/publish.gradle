apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = rootProject.file('version.txt').text.trim()
ext.isSnapshot = version.endsWith("SNAPSHOT")


task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

def pomConfig = {
	licenses {
		license {
			name 'The Apache Software License, Version 2.0'
			url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			distribution 'repo'
		}
	}
	developers {
		developer {
			id 'charliek'
			name 'Charlie Knudsen'
			email 'charlie.knudsen@gmail.com'
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'Opinionated kafka consumer')
				root.children().last() + pomConfig
			}
		}
	}
}

bintrayUpload { task ->
	doFirst {
		if (isSnapshot) {
			throw new GradleException('Cannot publish SNAPSHOT versions to BinTray!')
		}
	}
	gradle.taskGraph.whenReady { taskGraph ->
		if (taskGraph.hasTask(task)) {
			task.user = bintrayUser
			task.apiKey = bintrayKey
		}
	}
}

bintray {
	publications = ['mavenJava']
	dryRun = false
	publish = true
	pkg {
		repo = 'maven'
		name = 'com.charlieknudsen.konsumer'
		licenses = ['Apache-2.0']
		desc = 'Opinionated kafka consumer'
		labels = ['kafka']
		websiteUrl = 'https://github.com/charliek/konsumer'
		issueTrackerUrl = 'https://github.com/charliek/konsumer/issues'
		vcsUrl = 'https://github.com/charliek/konsumer.git'
		version {
			name = project.version
			vcsTag = project.version
		}
	}
}
